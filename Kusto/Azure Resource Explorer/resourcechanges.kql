// NSG port changes
resourcechanges
| extend
    changeTime = todatetime(properties.changeAttributes.timestamp),
    changeType = tostring(properties.changeType),
    changedBy = tostring(properties.changeAttributes.changedBy),
    resourceId = tostring(properties.targetResourceId),
    resourceType = tostring(properties.targetResourceType)
| where resourceType =~ "microsoft.network/networksecuritygroups"
| extend properties_changes_keys = bag_keys(properties.changes)
| mv-expand properties_changes_keys
| where properties_changes_keys contains "destinationPortRange"
| extend ports = properties.changes[tostring(properties_changes_keys)].newValue
| extend portRange = range(toint(split(ports, "-")[0]), toint(split(ports, "-")[1]))
| where ports contains "8081" or set_has_element(portRange, 8081)
| project
    changeTime,
    changeType,
    changedBy,
    resourceId,
    resourceType,
    changeDetails=properties.changes

// VMs created without certain tags
resourcechanges
| extend
    changeTime = todatetime(properties.changeAttributes.timestamp),
    changeType = tostring(properties.changeType),
    changedBy = tostring(properties.changeAttributes.changedBy),
    resourceId = tostring(properties.targetResourceId),
    resourceType = tostring(properties.targetResourceType)
| where changeType =~ "Create"
| where changeTime > ago(6h) and changeTime <= now()
| where resourceType =~ "microsoft.compute/virtualmachines" or resourceType =~ "microsoft.network/publicipaddresses"
| project createdTime= changeTime, resourceId, resourceType, createdBy=changedBy
| join (
    resources
    | where type =~ "microsoft.compute/virtualmachines" or type =~ "microsoft.network/publicipaddresses"
    | project resourceId=id, tags
    )
    on resourceId
| where isnull(tags.Owner) and isnull(tags.owner)
| project createdTime, resourceId, resourceType, createdBy, tags

// VM creation query
resourcechanges
| extend
    changeTime = todatetime(properties.changeAttributes.timestamp),
    changeType = tostring(properties.changeType),
    targetResourceType = tostring(properties.targetResourceType),
    changedBy = tostring(properties.changeAttributes.changedBy),
    CreatedResource = tostring(properties.targetResourceId),
    ResourceType = tostring(properties.targetResourceType)
| where changeType == "Create"
| where changeTime > ago(10m) and changeTime <= now()
| where ResourceType == "microsoft.compute/virtualmachines"
| project CreatedTime= changeTime, CreatedResource, ResourceType, CreatedBy=changedBy
| order by ['CreatedResource'] asc

// Change actor
resourcechanges
| extend
    changeTime = todatetime(properties.changeAttributes.timestamp),
    targetResourceId = tostring(properties.targetResourceId),
    changeType = tostring(properties.changeType),
    changedBy = tostring(properties.changeAttributes.changedBy),
    changedByType = properties.changeAttributes.changedByType,
    clientType = tostring(properties.changeAttributes.clientType)
| where changeTime > ago(7d)
| where changedByType in ("Application", "User")
| project changeTime, changeType, changedBy, changedByType, clientType, properties
| order by ['changeTime'] asc

// Attach/detach disks
resourcechanges
| extend
    changeTime = todatetime(properties.changeAttributes.timestamp),
    changeType = tostring(properties.changeType),
    changedBy = tostring(properties.changeAttributes.changedBy),
    resourceId = tostring(properties.targetResourceId),
    resourceType = tostring(properties.targetResourceType),
    diskState = properties.changes["properties.diskState"]
| where changeType =~ "Update" and resourceType =~ "microsoft.compute/disks"
//| where (diskState.previousValue =~ "Unattached" and diskState.newValue =~ "Attached") or (diskState.previousValue =~ "Attached" and diskState.newValue =~ "Unattached")
| where isnotnull(diskState)
| project changeTime, resourceId, diskState, changedBy

arg("").resourcechanges
| extend
    ChangeTime = todatetime(properties.changeAttributes.timestamp),
    ChangeType = tostring(properties.changeType),
    ChangedBy = tostring(properties.changeAttributes.changedBy),
    ResourceId = tostring(properties.targetResourceId),
    ResourceType = tostring(properties.targetResourceType),
    DiskState = properties.changes["properties.diskState"]
| where ChangeTime > ago(10m) and ChangeTime <= now()
| where ChangeType =~ "Update" and ResourceType =~ "microsoft.compute/disks"
| where (DiskState.previousValue =~ "Unattached" and DiskState.newValue =~ "Reserved") or (DiskState.previousValue =~ "Reserved" and DiskState.newValue =~ "Unattached")
| where ChangedBy contains "@inspirebrands.com" or ChangedBy == "System"
| project ChangeTime, ResourceId, DiskState, ChangedBy


resourcechanges
| extend
    changeTime = todatetime(properties.changeAttributes.timestamp),
    changeType = tostring(properties.changeType),
    changedBy = tostring(properties.changeAttributes.changedBy),
    resourceId = tostring(properties.targetResourceId),
    resourceType = tostring(properties.targetResourceType),
    diskState = properties.changes["properties.diskState"]
| where resourceType =~ "microsoft.compute/disks"
| where resourceId contains ""
| project changeTime, resourceId, diskState, changedBy, properties


resourcechanges
| extend
    changeTime = todatetime(properties.changeAttributes.timestamp),
    changeType = tostring(properties.changeType),
    changedBy = tostring(properties.changeAttributes.changedBy),
    resourceId = tostring(properties.targetResourceId),
    resourceType = tostring(properties.targetResourceType)
| where resourceType =~ "microsoft.compute/virtualmachines"
| where changeType == "Update"
| where properties.changes contains "dataDisks"
| project changeTime, resourceId, changedBy, properties


arg("").resourcechanges
| extend
    ChangeTime = todatetime(properties.changeAttributes.timestamp),
    ChangeType = tostring(properties.changeType),
    ChangedBy = tostring(properties.changeAttributes.changedBy),
    ResourceId = tostring(properties.targetResourceId),
    ResourceType = tostring(properties.targetResourceType)
| where ChangeTime > ago(10m) and ChangeTime <= now()
| where ChangeType =~ "Update" and ResourceType =~ "microsoft.compute/virtualmachines"
| where properties.changes contains "dataDisks"
| project ChangeTime, ResourceId, ChangedBy